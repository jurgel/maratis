# Maratis Sample app build Sconscript
# $LastChangedDate: 2011-09-27 19:11:42 +0100 (Tue, 27 Sep 2011) $
# $LastChangedBy: anael.seghezzi@gmail.com $


import os, sys, fnmatch
import pyutils
import thirdparty

Import('env')
env_local = env.Clone()

# include dirs
env_local.AppendUnique(CPPPATH = ['#MSDK/MCore/Includes',
                                  '#MSDK/MEngine/Includes',
                                  '#MSDK/MGui/Includes',
								  '#Maratis/Common',
								  '#Maratis/Common/_Implements'] )
# lib dirs
env_local.AppendUnique(LIBPATH = ['../../MSDK/MaratisCore',
                                  '../../MSDK/MEngine',
                                  '../../MSDK/MCore',
                                  '../../MSDK/MGui',
                                  '../../Maratis/Common'] )
# needed libs
env_local.AppendUnique(LIBS = ['MaratisCommon',
                               'MEngine',
                               'MCore',
                               'MGui'] )

if sys.platform=='linux2' or sys.platform=='linux3':
    env_local.AppendUnique(LIBS = ['GL',
                                   'Xxf86vm'] )

# add 3rdparty libraries parameters
thirdparty.npk_dev.addNPKDevToEnv(env_local)
thirdparty.lua.addLuaToEnv(env_local)
thirdparty.bullet.addBulletToEnv(env_local)
thirdparty.openal.addOpenALToEnv(env_local)
thirdparty.opengl.addOpenGLToEnv(env_local)
thirdparty.glee.addGleeToEnv(env_local)
thirdparty.devil.addDevilToEnv(env_local)
thirdparty.tinyxml.addTinyXmlToEnv(env_local)
thirdparty.libpng.addLibPngToEnv(env_local)
thirdparty.libjpeg.addLibJpegToEnv(env_local)
thirdparty.zlib.addZlibToEnv(env_local)
thirdparty.freetype.addFreetypeToEnv(env_local)
thirdparty.libsndfile.addLibSndFileToEnv(env_local)
if sys.platform=='darwin':
    thirdparty.cocoa.addCocoaToEnv(env_local)
if sys.platform=='win32':
    thirdparty.windows.addWindowsToEnv(env_local)

# get list of files (recursive)
files = pyutils.getFilesRecursive(Dir('.').srcnode().abspath,'*.c*')

# build application
env_local.AppendUnique(RPATH = ['@loader_path/.',
                                '@loader_path/.'])
manualuse = env_local.Program('ManualUse',files,PDB='ManualUse.pdb')


# ####### #
# Install #
# ####### #

# install to prod dir
finaldir = os.path.join(env['proddir'],'Examples','ManualUse')
env_local.Install(finaldir, manualuse)
env_local.Install(finaldir, '#Examples/Data/meshs')

# Mac specific: app bundle creation and installation
if sys.platform == 'darwin':
    manualuse = env.MakeBundle ( 'ManualUse.app', 
                                 manualuse,
                                 typecode = 'APPL',
                                 resources = ['#Examples/Data/meshs'])
    env_local.InstallBundle(finaldir, manualuse)


Return('manualuse')
